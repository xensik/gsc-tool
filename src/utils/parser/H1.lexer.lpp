/* Copyright 2021 xensik. All rights reserved.
//
// Use of this source code is governed by a GNU GPLv3 license
// that can be found in the LICENSE file.
*/

%option outfile="./H1/compiler_lexer.cpp"
%option header-file="./H1/compiler_lexer.hpp"
%option prefix = "H1"
%option reentrant
%option noyywrap batch nounput noinput
%option never-interactive
%option nounistd

%{
#include "H1.hpp"
#include "compiler_parser.hpp"
%}

RGX_FILEPATH        ([_A-Za-z0-9]+\\)+[_A-Za-z0-9]+
RGX_IDENTIFIER      [_A-Za-z][_A-Za-z0-9\-]*
RGX_STRING          \"(?:\\.|[^\"])*?\"|\'(?:\\.|[^\'])*?\'
RGX_FLOAT           -?[0-9]+\.(?:[0-9]*)?f?|\.[0-9]+f?
RGX_INTEGER         -?[0-9]+
RGX_DEFAULT         (.|\n)

%x COMMENT_BLOCK_STATE
%x DEVELOPER_BLOCK_STATE

%%

[ \t\n\r]

"//".*

"/*"                         { BEGIN(COMMENT_BLOCK_STATE); }
<COMMENT_BLOCK_STATE>(.|\n)
<COMMENT_BLOCK_STATE>"*/"    { BEGIN(INITIAL); }

"/#"                         { BEGIN(DEVELOPER_BLOCK_STATE); }
<DEVELOPER_BLOCK_STATE>(.|\n)
<DEVELOPER_BLOCK_STATE>"#/"  { BEGIN(INITIAL); }

"#include"          { return H1::parser::make_INCLUDE(); }
"#using_animtree"   { return H1::parser::make_USING_ANIMTREE(); }
"#animtree"         { return H1::parser::make_ANIMTREE(); }
"endon"             { return H1::parser::make_ENDON(); }
"notify"            { return H1::parser::make_NOTIFY(); }
"wait"              { return H1::parser::make_WAIT(); }
"waittill"          { return H1::parser::make_WAITTILL(); }
"waittillmatch"     { return H1::parser::make_WAITTILLMATCH(); }
"waittillframeend"  { return H1::parser::make_WAITTILLFRAMEEND(); }
"waitframe"         { return H1::parser::make_WAITFRAME(); }
"if"                { return H1::parser::make_IF(); }
"else"              { return H1::parser::make_ELSE(); }
"while"             { return H1::parser::make_WHILE(); }
"for"               { return H1::parser::make_FOR(); }
"foreach"           { return H1::parser::make_FOREACH(); }
"in"                { return H1::parser::make_IN(); }
"switch"            { return H1::parser::make_SWITCH(); }
"case"              { return H1::parser::make_CASE(); }
"default"           { return H1::parser::make_DEFAULT(); }
"break"             { return H1::parser::make_BREAK(); }
"continue"          { return H1::parser::make_CONTINUE(); }
"return"            { return H1::parser::make_RETURN(); }
"thread"            { return H1::parser::make_THREAD(); }
"call"              { return H1::parser::make_CALL(); }
"true"              { return H1::parser::make_TRUE(); }
"false"             { return H1::parser::make_FALSE(); }
"undefined"         { return H1::parser::make_UNDEFINED(); }
"size"              { return H1::parser::make_SIZE(); }
"game"              { return H1::parser::make_GAME(); }
"self"              { return H1::parser::make_SELF(); }
"anim"              { return H1::parser::make_ANIM(); }
"level"             { return H1::parser::make_LEVEL(); }
"[]"                { return H1::parser::make_EMPTY_ARRAY(); }
\(                  { return H1::parser::make_LPAREN(); }
\)                  { return H1::parser::make_RPAREN(); }
\{                  { return H1::parser::make_LBRACE(); }
\}                  { return H1::parser::make_RBRACE(); }
\[                  { return H1::parser::make_LBRACKET(); }
\]                  { return H1::parser::make_RBRACKET(); }
\,                  { return H1::parser::make_COMMA(); }
\.                  { return H1::parser::make_DOT(); }
\:\:                { return H1::parser::make_DOUBLECOLON(); }
\:                  { return H1::parser::make_COLON(); }
\;                  { return H1::parser::make_SEMICOLON(); }
\?                  { return H1::parser::make_QMARK(); }
\+\+                { return H1::parser::make_INCREMENT(); }
\-\-                { return H1::parser::make_DECREMENT(); }
\<\<\=              { return H1::parser::make_ASSIGN_LSHIFT(); }
\>\>\=              { return H1::parser::make_ASSIGN_RSHIFT(); }
\<\<                { return H1::parser::make_LSHIFT(); }
\>\>                { return H1::parser::make_RSHIFT(); }
\|\|                { return H1::parser::make_OR(); }
\&\&                { return H1::parser::make_AND(); }
\=\=                { return H1::parser::make_EQUALITY(); }
\!\=                { return H1::parser::make_INEQUALITY(); }
\<\=                { return H1::parser::make_LESS_EQUAL(); }
\>\=                { return H1::parser::make_GREATER_EQUAL(); }
\<                  { return H1::parser::make_LESS(); }
\>                  { return H1::parser::make_GREATER(); }
\+\=                { return H1::parser::make_ASSIGN_ADD(); }
\-\=                { return H1::parser::make_ASSIGN_SUB(); }
\*\=                { return H1::parser::make_ASSIGN_MULT(); }
\/\=                { return H1::parser::make_ASSIGN_DIV(); }
\%\=                { return H1::parser::make_ASSIGN_MOD(); }
\|\=                { return H1::parser::make_ASSIGN_BITWISE_OR(); }
\&\=                { return H1::parser::make_ASSIGN_BITWISE_AND(); }
\^\=                { return H1::parser::make_ASSIGN_BITWISE_EXOR(); }
\=                  { return H1::parser::make_ASSIGN(); }
\+                  { return H1::parser::make_ADD(); }
\-                  { return H1::parser::make_SUB(); }
\*                  { return H1::parser::make_MULT(); }
\/                  { return H1::parser::make_DIV(); }
\%                  { return H1::parser::make_MOD(); }
\!                  { return H1::parser::make_NOT(); }
\~                  { return H1::parser::make_COMPLEMENT(); }
\|                  { return H1::parser::make_BITWISE_OR(); }
\&                  { return H1::parser::make_BITWISE_AND(); }
\^                  { return H1::parser::make_BITWISE_EXOR(); }

{RGX_FILEPATH}      { return H1::parser::make_FILEPATH(std::string(yytext)); }
{RGX_IDENTIFIER}    { return H1::parser::make_IDENTIFIER(utils::string::to_lower(yytext)); }
\&{RGX_STRING}      { return H1::parser::make_STRING_LOC(std::string(yytext)); }
{RGX_STRING}        { return H1::parser::make_STRING(std::string(yytext)); }
{RGX_FLOAT}         { return H1::parser::make_FLOAT(std::string(yytext)); }
{RGX_INTEGER}       { return H1::parser::make_INTEGER(std::string(yytext)); }

<<EOF>>             { return H1::parser::make_H1EOF(); }
<*>{RGX_DEFAULT}    { throw H1::parser::syntax_error("invalid character: " + std::string(yytext)); }
%%
