/* Copyright 2021 xensik. All rights reserved.
//
// Use of this source code is governed by a GNU GPLv3 license
// that can be found in the LICENSE file.
*/

%option outfile="./S1/compiler_lexer.cpp"
%option header-file="./S1/compiler_lexer.hpp"
%option prefix = "S1"
%option reentrant
%option case-insensitive
%option noyywrap batch nounput noinput
%option never-interactive
%option nounistd

%top{
#include "S1.hpp"
#include "compiler_parser.hpp"
#ifndef YY_NO_UNISTD_H
#define YY_NO_UNISTD_H
#endif
}

RGX_FILEPATH        ([_A-Za-z0-9]+\\)+[_A-Za-z0-9]+
RGX_IDENTIFIER      [_A-Za-z][_A-Za-z0-9\-]*
RGX_ANIMREF         \%[_A-Za-z][_A-Za-z0-9\-]*
RGX_STRING          \"(?:\\.|[^\"])*?\"|\'(?:\\.|[^\'])*?\'
RGX_FLOAT           -?[0-9]+\.(?:[0-9]*)?f?|\.[0-9]+f?
RGX_INTEGER         -?[0-9]+
RGX_DEFAULT         (.|\n)

%x COMMENT_BLOCK_STATE
%x DEVELOPER_BLOCK_STATE

%%

[ \t\r]
\n                          { (*location)++; }
"//".*

"/*"					    { BEGIN(COMMENT_BLOCK_STATE); }
<COMMENT_BLOCK_STATE>.
<COMMENT_BLOCK_STATE>\n     { (*location)++; }
<COMMENT_BLOCK_STATE>"*/"	{ BEGIN(INITIAL); }

"/#"				        { BEGIN(DEVELOPER_BLOCK_STATE); }
<DEVELOPER_BLOCK_STATE>.
<DEVELOPER_BLOCK_STATE>\n     { (*location)++; }
<DEVELOPER_BLOCK_STATE>"#/"	{ BEGIN(INITIAL); }

"#include"          { return S1::parser::make_INCLUDE(); }
"#using_animtree"   { return S1::parser::make_USING_ANIMTREE(); }
"#animtree"         { return S1::parser::make_ANIMTREE(); }
"endon"             { return S1::parser::make_ENDON(); }
"notify"            { return S1::parser::make_NOTIFY(); }
"wait"              { return S1::parser::make_WAIT(); }
"waittill"          { return S1::parser::make_WAITTILL(); }
"waittillmatch"     { return S1::parser::make_WAITTILLMATCH(); }
"waittillframeend"  { return S1::parser::make_WAITTILLFRAMEEND(); }
"waitframe"         { return S1::parser::make_WAITFRAME(); }
"if"                { return S1::parser::make_IF(); }
"else"              { return S1::parser::make_ELSE(); }
"while"             { return S1::parser::make_WHILE(); }
"for"               { return S1::parser::make_FOR(); }
"foreach"           { return S1::parser::make_FOREACH(); }
"in"                { return S1::parser::make_IN(); }
"switch"            { return S1::parser::make_SWITCH(); }
"case"              { return S1::parser::make_CASE(); }
"default"           { return S1::parser::make_DEFAULT(); }
"break"             { return S1::parser::make_BREAK(); }
"continue"          { return S1::parser::make_CONTINUE(); }
"return"            { return S1::parser::make_RETURN(); }
"thread"            { return S1::parser::make_THREAD(); }
"call"              { return S1::parser::make_CALL(); }
"true"              { return S1::parser::make_TRUE(); }
"false"             { return S1::parser::make_FALSE(); }
"undefined"         { return S1::parser::make_UNDEFINED(); }
"size"              { return S1::parser::make_SIZE(); }
"game"              { return S1::parser::make_GAME(); }
"self"              { return S1::parser::make_SELF(); }
"anim"              { return S1::parser::make_ANIM(); }
"level"             { return S1::parser::make_LEVEL(); }
"[]"                { return S1::parser::make_EMPTY_ARRAY(); }
\(                  { return S1::parser::make_LPAREN(); }
\)                  { return S1::parser::make_RPAREN(); }
\{                  { return S1::parser::make_LBRACE(); }
\}                  { return S1::parser::make_RBRACE(); }
\[                  { return S1::parser::make_LBRACKET(); }
\]                  { return S1::parser::make_RBRACKET(); }
\,                  { return S1::parser::make_COMMA(); }
\.                  { return S1::parser::make_DOT(); }
\:\:                { return S1::parser::make_DOUBLECOLON(); }
\:                  { return S1::parser::make_COLON(); }
\;                  { return S1::parser::make_SEMICOLON(); }
\?                  { return S1::parser::make_QMARK(); }
\+\+                { return S1::parser::make_INCREMENT(); }
\-\-                { return S1::parser::make_DECREMENT(); }
\<\<\=              { return S1::parser::make_ASSIGN_LSHIFT(); }
\>\>\=              { return S1::parser::make_ASSIGN_RSHIFT(); }
\<\<                { return S1::parser::make_LSHIFT(); }
\>\>                { return S1::parser::make_RSHIFT(); }
\|\|                { return S1::parser::make_OR(); }
\&\&                { return S1::parser::make_AND(); }
\=\=                { return S1::parser::make_EQUALITY(); }
\!\=                { return S1::parser::make_INEQUALITY(); }
\<\=                { return S1::parser::make_LESS_EQUAL(); }
\>\=                { return S1::parser::make_GREATER_EQUAL(); }
\<                  { return S1::parser::make_LESS(); }
\>                  { return S1::parser::make_GREATER(); }
\+\=                { return S1::parser::make_ASSIGN_ADD(); }
\-\=                { return S1::parser::make_ASSIGN_SUB(); }
\*\=                { return S1::parser::make_ASSIGN_MULT(); }
\/\=                { return S1::parser::make_ASSIGN_DIV(); }
\%\=                { return S1::parser::make_ASSIGN_MOD(); }
\|\=                { return S1::parser::make_ASSIGN_BITWISE_OR(); }
\&\=                { return S1::parser::make_ASSIGN_BITWISE_AND(); }
\^\=                { return S1::parser::make_ASSIGN_BITWISE_EXOR(); }
\=                  { return S1::parser::make_ASSIGN(); }
\+                  { return S1::parser::make_ADD(); }
\-                  { return S1::parser::make_SUB(); }
\*                  { return S1::parser::make_MULT(); }
\/                  { return S1::parser::make_DIV(); }
\%                  { return S1::parser::make_MOD(); }
\!                  { return S1::parser::make_NOT(); }
\~                  { return S1::parser::make_COMPLEMENT(); }
\|                  { return S1::parser::make_BITWISE_OR(); }
\&                  { return S1::parser::make_BITWISE_AND(); }
\^                  { return S1::parser::make_BITWISE_EXOR(); }

{RGX_FILEPATH}      { return S1::parser::make_FILEPATH(std::string(yytext)); }
{RGX_IDENTIFIER}    { return S1::parser::make_IDENTIFIER(utils::string::to_lower(yytext)); }
{RGX_ANIMREF}       { return S1::parser::make_ANIMREF(utils::string::to_lower(yytext).substr(1)); }
\&{RGX_STRING}      { return S1::parser::make_STRING_LOC(std::string(yytext).substr(1)); }
{RGX_STRING}        { return S1::parser::make_STRING(std::string(yytext)); }
{RGX_FLOAT}         { return S1::parser::make_FLOAT(std::string(yytext)); }
{RGX_INTEGER}       { return S1::parser::make_INTEGER(std::string(yytext)); }

<<EOF>>             { return S1::parser::make_S1EOF(); }
<*>{RGX_DEFAULT}    { throw S1::parser::syntax_error("invalid character: " + std::string(yytext)); }
%%
