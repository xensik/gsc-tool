/* Copyright 2021 xensik. All rights reserved.
//
// Use of this source code is governed by a GNU GPLv3 license
// that can be found in the LICENSE file.
*/

%option outfile="./H2/compiler_lexer.cpp"
%option header-file="./H2/compiler_lexer.hpp"
%option prefix = "H2"
%option reentrant
%option case-insensitive
%option noyywrap batch nounput noinput
%option never-interactive
%option nounistd

%top{
#include "H2.hpp"
#include "compiler_parser.hpp"
#ifndef YY_NO_UNISTD_H
#define YY_NO_UNISTD_H
#endif
}

RGX_FILEPATH        ([_A-Za-z0-9]+\\)+[_A-Za-z0-9]+
RGX_IDENTIFIER      [_A-Za-z][_A-Za-z0-9\-]*
RGX_ANIMREF         \%[_A-Za-z][_A-Za-z0-9\-]*
RGX_STRING          \"(?:\\.|[^\"])*?\"|\'(?:\\.|[^\'])*?\'
RGX_FLOAT           -?[0-9]+\.(?:[0-9]*)?f?|\.[0-9]+f?
RGX_INTEGER         -?[0-9]+
RGX_DEFAULT         (.|\n)

%x COMMENT_BLOCK_STATE
%x DEVELOPER_BLOCK_STATE

%%

[ \t\r]

\n                          { (*location)++; }

"//".*

"/*"					    { BEGIN(COMMENT_BLOCK_STATE); }
<COMMENT_BLOCK_STATE>.
<COMMENT_BLOCK_STATE>\n     { (*location)++; }
<COMMENT_BLOCK_STATE>"*/"	{ BEGIN(INITIAL); }

"/#"				        { BEGIN(DEVELOPER_BLOCK_STATE); }
<DEVELOPER_BLOCK_STATE>.
<DEVELOPER_BLOCK_STATE>\n     { (*location)++; }
<DEVELOPER_BLOCK_STATE>"#/"	{ BEGIN(INITIAL); }	

"#include"          { return H2::parser::make_INCLUDE(); }
"#using_animtree"   { return H2::parser::make_USING_ANIMTREE(); }
"#animtree"         { return H2::parser::make_ANIMTREE(); }
"endon"             { return H2::parser::make_ENDON(); }
"notify"            { return H2::parser::make_NOTIFY(); }
"wait"              { return H2::parser::make_WAIT(); }
"waittill"          { return H2::parser::make_WAITTILL(); }
"waittillmatch"     { return H2::parser::make_WAITTILLMATCH(); }
"waittillframeend"  { return H2::parser::make_WAITTILLFRAMEEND(); }
"waitframe"         { return H2::parser::make_WAITFRAME(); }
"if"                { return H2::parser::make_IF(); }
"else"              { return H2::parser::make_ELSE(); }
"while"             { return H2::parser::make_WHILE(); }
"for"               { return H2::parser::make_FOR(); }
"foreach"           { return H2::parser::make_FOREACH(); }
"in"                { return H2::parser::make_IN(); }
"switch"            { return H2::parser::make_SWITCH(); }
"case"              { return H2::parser::make_CASE(); }
"default"           { return H2::parser::make_DEFAULT(); }
"break"             { return H2::parser::make_BREAK(); }
"continue"          { return H2::parser::make_CONTINUE(); }
"return"            { return H2::parser::make_RETURN(); }
"thread"            { return H2::parser::make_THREAD(); }
"call"              { return H2::parser::make_CALL(); }
"true"              { return H2::parser::make_TRUE(); }
"false"             { return H2::parser::make_FALSE(); }
"undefined"         { return H2::parser::make_UNDEFINED(); }
"size"              { return H2::parser::make_SIZE(); }
"game"              { return H2::parser::make_GAME(); }
"self"              { return H2::parser::make_SELF(); }
"anim"              { return H2::parser::make_ANIM(); }
"level"             { return H2::parser::make_LEVEL(); }
"[]"                { return H2::parser::make_EMPTY_ARRAY(); }
\(                  { return H2::parser::make_LPAREN(); }
\)                  { return H2::parser::make_RPAREN(); }
\{                  { return H2::parser::make_LBRACE(); }
\}                  { return H2::parser::make_RBRACE(); }
\[                  { return H2::parser::make_LBRACKET(); }
\]                  { return H2::parser::make_RBRACKET(); }
\,                  { return H2::parser::make_COMMA(); }
\.                  { return H2::parser::make_DOT(); }
\:\:                { return H2::parser::make_DOUBLECOLON(); }
\:                  { return H2::parser::make_COLON(); }
\;                  { return H2::parser::make_SEMICOLON(); }
\?                  { return H2::parser::make_QMARK(); }
\+\+                { return H2::parser::make_INCREMENT(); }
\-\-                { return H2::parser::make_DECREMENT(); }
\<\<\=              { return H2::parser::make_ASSIGN_LSHIFT(); }
\>\>\=              { return H2::parser::make_ASSIGN_RSHIFT(); }
\<\<                { return H2::parser::make_LSHIFT(); }
\>\>                { return H2::parser::make_RSHIFT(); }
\|\|                { return H2::parser::make_OR(); }
\&\&                { return H2::parser::make_AND(); }
\=\=                { return H2::parser::make_EQUALITY(); }
\!\=                { return H2::parser::make_INEQUALITY(); }
\<\=                { return H2::parser::make_LESS_EQUAL(); }
\>\=                { return H2::parser::make_GREATER_EQUAL(); }
\<                  { return H2::parser::make_LESS(); }
\>                  { return H2::parser::make_GREATER(); }
\+\=                { return H2::parser::make_ASSIGN_ADD(); }
\-\=                { return H2::parser::make_ASSIGN_SUB(); }
\*\=                { return H2::parser::make_ASSIGN_MULT(); }
\/\=                { return H2::parser::make_ASSIGN_DIV(); }
\%\=                { return H2::parser::make_ASSIGN_MOD(); }
\|\=                { return H2::parser::make_ASSIGN_BITWISE_OR(); }
\&\=                { return H2::parser::make_ASSIGN_BITWISE_AND(); }
\^\=                { return H2::parser::make_ASSIGN_BITWISE_EXOR(); }
\=                  { return H2::parser::make_ASSIGN(); }
\+                  { return H2::parser::make_ADD(); }
\-                  { return H2::parser::make_SUB(); }
\*                  { return H2::parser::make_MULT(); }
\/                  { return H2::parser::make_DIV(); }
\%                  { return H2::parser::make_MOD(); }
\!                  { return H2::parser::make_NOT(); }
\~                  { return H2::parser::make_COMPLEMENT(); }
\|                  { return H2::parser::make_BITWISE_OR(); }
\&                  { return H2::parser::make_BITWISE_AND(); }
\^                  { return H2::parser::make_BITWISE_EXOR(); }

{RGX_FILEPATH}      { return H2::parser::make_FILEPATH(std::string(yytext)); }
{RGX_IDENTIFIER}    { return H2::parser::make_IDENTIFIER(utils::string::to_lower(yytext)); }
{RGX_ANIMREF}       { return H2::parser::make_ANIMREF(utils::string::to_lower(yytext).substr(1)); }
\&{RGX_STRING}      { return H2::parser::make_STRING_LOC(std::string(yytext).substr(1)); }
{RGX_STRING}        { return H2::parser::make_STRING(std::string(yytext)); }
{RGX_FLOAT}         { return H2::parser::make_FLOAT(std::string(yytext)); }
{RGX_INTEGER}       { return H2::parser::make_INTEGER(std::string(yytext)); }

<<EOF>>             { return H2::parser::make_H2EOF(); }
<*>{RGX_DEFAULT}    { throw H2::parser::syntax_error("invalid character: " + std::string(yytext)); }
%%
