/* Copyright 2021 xensik. All rights reserved.
//
// Use of this source code is governed by a GNU GPLv3 license
// that can be found in the LICENSE file.
*/

%option outfile="./S2/compiler_lexer.cpp"
%option header-file="./S2/compiler_lexer.hpp"
%option reentrant
%option noyywrap batch nounput noinput
%option never-interactive
%option nounistd

%{
#include "S2.hpp"
#include "compiler_parser.hpp"
%}

RGX_FILEPATH        ([_A-Za-z0-9]+\\)+[_A-Za-z0-9]+
RGX_IDENTIFIER      [_A-Za-z][_A-Za-z0-9\-]*
RGX_STRING          \"(?:\\.|[^\"])*?\"|\'(?:\\.|[^\'])*?\'
RGX_FLOAT           -?[0-9]+\.(?:[0-9]*)?f?|\.[0-9]+f?
RGX_INTEGER         -?[0-9]+
RGX_DEFAULT         (.|\n)

%x COMMENT_BLOCK_STATE
%x DEVELOPER_BLOCK_STATE

%%

[ \t\n\r]

"//".*

"/*"                         { BEGIN(COMMENT_BLOCK_STATE); }
<COMMENT_BLOCK_STATE>(.|\n)
<COMMENT_BLOCK_STATE>"*/"    { BEGIN(INITIAL); }

"/#"                         { BEGIN(DEVELOPER_BLOCK_STATE); }
<DEVELOPER_BLOCK_STATE>(.|\n)
<DEVELOPER_BLOCK_STATE>"#/"  { BEGIN(INITIAL); }

"#include"          { return S2::parser::make_INCLUDE(); }
"#using_animtree"   { return S2::parser::make_USING_ANIMTREE(); }
"#animtree"         { return S2::parser::make_ANIMTREE(); }
"endon"             { return S2::parser::make_ENDON(); }
"notify"            { return S2::parser::make_NOTIFY(); }
"wait"              { return S2::parser::make_WAIT(); }
"waittill"          { return S2::parser::make_WAITTILL(); }
"waittillmatch"     { return S2::parser::make_WAITTILLMATCH(); }
"waittillframeend"  { return S2::parser::make_WAITTILLFRAMEEND(); }
"waitframe"         { return S2::parser::make_WAITFRAME(); }
"if"                { return S2::parser::make_IF(); }
"else"              { return S2::parser::make_ELSE(); }
"while"             { return S2::parser::make_WHILE(); }
"for"               { return S2::parser::make_FOR(); }
"foreach"           { return S2::parser::make_FOREACH(); }
"in"                { return S2::parser::make_IN(); }
"switch"            { return S2::parser::make_SWITCH(); }
"case"              { return S2::parser::make_CASE(); }
"default"           { return S2::parser::make_DEFAULT(); }
"break"             { return S2::parser::make_BREAK(); }
"continue"          { return S2::parser::make_CONTINUE(); }
"return"            { return S2::parser::make_RETURN(); }
"thread"            { return S2::parser::make_THREAD(); }
"call"              { return S2::parser::make_CALL(); }
"true"              { return S2::parser::make_TRUE(); }
"false"             { return S2::parser::make_FALSE(); }
"undefined"         { return S2::parser::make_UNDEFINED(); }
"size"              { return S2::parser::make_SIZE(); }
"game"              { return S2::parser::make_GAME(); }
"self"              { return S2::parser::make_SELF(); }
"anim"              { return S2::parser::make_ANIM(); }
"level"             { return S2::parser::make_LEVEL(); }
"[]"                { return S2::parser::make_EMPTY_ARRAY(); }
\(                  { return S2::parser::make_LPAREN(); }
\)                  { return S2::parser::make_RPAREN(); }
\{                  { return S2::parser::make_LBRACE(); }
\}                  { return S2::parser::make_RBRACE(); }
\[                  { return S2::parser::make_LBRACKET(); }
\]                  { return S2::parser::make_RBRACKET(); }
\,                  { return S2::parser::make_COMMA(); }
\.                  { return S2::parser::make_DOT(); }
\:\:                { return S2::parser::make_DOUBLECOLON(); }
\:                  { return S2::parser::make_COLON(); }
\;                  { return S2::parser::make_SEMICOLON(); }
\?                  { return S2::parser::make_QMARK(); }
\+\+                { return S2::parser::make_INCREMENT(); }
\-\-                { return S2::parser::make_DECREMENT(); }
\<\<\=              { return S2::parser::make_ASSIGN_LSHIFT(); }
\>\>\=              { return S2::parser::make_ASSIGN_RSHIFT(); }
\<\<                { return S2::parser::make_LSHIFT(); }
\>\>                { return S2::parser::make_RSHIFT(); }
\|\|                { return S2::parser::make_OR(); }
\&\&                { return S2::parser::make_AND(); }
\=\=                { return S2::parser::make_EQUALITY(); }
\!\=                { return S2::parser::make_INEQUALITY(); }
\<\=                { return S2::parser::make_LESS_EQUAL(); }
\>\=                { return S2::parser::make_GREATER_EQUAL(); }
\<                  { return S2::parser::make_LESS(); }
\>                  { return S2::parser::make_GREATER(); }
\+\=                { return S2::parser::make_ASSIGN_ADD(); }
\-\=                { return S2::parser::make_ASSIGN_SUB(); }
\*\=                { return S2::parser::make_ASSIGN_MULT(); }
\/\=                { return S2::parser::make_ASSIGN_DIV(); }
\%\=                { return S2::parser::make_ASSIGN_MOD(); }
\|\=                { return S2::parser::make_ASSIGN_BITWISE_OR(); }
\&\=                { return S2::parser::make_ASSIGN_BITWISE_AND(); }
\^\=                { return S2::parser::make_ASSIGN_BITWISE_EXOR(); }
\=                  { return S2::parser::make_ASSIGN(); }
\+                  { return S2::parser::make_ADD(); }
\-                  { return S2::parser::make_SUB(); }
\*                  { return S2::parser::make_MULT(); }
\/                  { return S2::parser::make_DIV(); }
\%                  { return S2::parser::make_MOD(); }
\!                  { return S2::parser::make_NOT(); }
\~                  { return S2::parser::make_COMPLEMENT(); }
\|                  { return S2::parser::make_BITWISE_OR(); }
\&                  { return S2::parser::make_BITWISE_AND(); }
\^                  { return S2::parser::make_BITWISE_EXOR(); }

{RGX_FILEPATH}      { return S2::parser::make_FILEPATH(std::string(yytext)); }
{RGX_IDENTIFIER}    { return S2::parser::make_IDENTIFIER(utils::string::to_lower(yytext)); }
\&{RGX_STRING}      { return S2::parser::make_STRING_LOC(std::string(yytext)); }
{RGX_STRING}        { return S2::parser::make_STRING(std::string(yytext)); }
{RGX_FLOAT}         { return S2::parser::make_FLOAT(std::string(yytext)); }
{RGX_INTEGER}       { return S2::parser::make_INTEGER(std::string(yytext)); }

<<EOF>>             { return S2::parser::make_YYEOF(); }
<*>{RGX_DEFAULT}    { throw S2::parser::syntax_error("invalid character: " + std::string(yytext)); }
%%
